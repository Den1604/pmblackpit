% This map is the one between each trap, it uses *Dungeon_CurrentBase to know the next map to load, the ones who take to
% each boss room are mze_07 & mze_08
#import Maze_FuzzyChest.mpat
#import Maze_Misc.mpat % Fog Function
#import Maze_MarioHPTracker.mpat % track if mario have 0hp

#new:Header $Header
{
[MainScript] $Script_Main
[Background] 80200000
[EntryList] $EntryList
[EntryCount] 3
[MapTattle]  $Function_GetTattle
}

#new:Function_Init $Function_Init
{
PUSH 	RA, A0, A1
LIA 	A0, 800B0CF0
LIA 	A1, "tik_tex" 
JAL 	~Func:sprintf
RESERVED
COPY 	V0, R0
JPOP 	RA, A0, A1
}

#new:EntryList $EntryList
{
~Vec4f:Entry0
~Vec4f:Entry1
~Vec4f:Entry2
}

#new:Script_Main $Script_Main
{
Call    SetSpriteShading  	( FFFFFFFF )
Call    SetCamPerspective 	( .Cam:Default 00000003 00000019 00000010 00001000 )
Call    SetCamBGColor     	( .Cam:Default .Cam:Default .Cam:Default .Cam:Default )
Call    SetCamEnabled       ( .Cam:Default .True )
Call    SetCamLeadPlayer    ( .Cam:Default .False )
Call    $Function_SetupFog
Call    ModifyColliderFlags ( .False ~Collider:ChestL 7FFFFE00 )
Call    ModifyColliderFlags ( .False ~Collider:ChestR 7FFFFE00 )
Call    $ReadAddress    ( $Maze_ChestData_Special2 0 *Var[0] .False .False )
If *Var[0] == *Dungeon_MapID % giant chest room
    Call    MakeNpcs ( 0 $NpcGroupList_Fuzzies )
    ExecWait $Script_MakeEntities_FuzzyFloor
    % Unlock Chest?
    Set *Var[0] *Flag_Maze_Fuzzy1
    Add *Var[0] *Flag_Maze_Fuzzy2
    Add *Var[0] *Flag_Maze_Fuzzy3
    If *Var[0] < 3
        If *Flag_Maze_SpecialChest2 == .False
            If *AreaByte[0] == 0 % Left
                Call    ModifyColliderFlags ( .True ~Collider:ChestL 7FFFFE00 )
            Else % Right
                Call    ModifyColliderFlags ( .True ~Collider:ChestR 7FFFFE00 )
            EndIf
        EndIf
    EndIf
EndIf
Set	   *Var[0] $Script_MakeExits
Exec    EnterWalk
Set     *Dungeon_MapID 7
Wait    1
Exec    $Script_KillMario % track mario hp
Return
End
}

#new:Script $Script_MakeExits
{
    Bind     $Script_Exit0  .Trigger:FloorAbove ~Collider:Entry0 1 0
    Bind     $Script_Exit1  .Trigger:FloorAbove ~Collider:Entry1 1 0
    Bind     $Script_Exit2  .Trigger:FloorAbove ~Collider:Entry2 1 0
    Return
    End
}

#new:Script $Script_Exit0
{
    Call     UseExitHeading  	( 3C 0 )
    Exec     80285CF4
    Set     *Dungeon_MapID  6
    Call     GotoMap        ( "mze_09" 1 )
    Wait     100`
    Return
    End
}

#new:Script $Script_Exit1
{
    Call     UseExitHeading  	( 3C 1 )
    Exec     80285CF4
    Set     *Dungeon_MapID  8
    Call     GotoMap        ( "mze_09" 0 )
    Wait     100` 
    Return
    End
}

#new:Script $Script_Exit2
{
    Call     UseExitHeading  	( 3C 2 )
    Exec     80285CF4 
    Set     *Dungeon_MapID  17`
    Call     GotoMap        ( "mze_09" 0 )
    Wait     100` 
    Return
    End
}

#new:Function $Function_GetTattle
{
LIO       V0, $MapTattle
JR        RA
RESERVED
}
	
#string $MapTattle
{
[STYLE:TATTLE][...]
[Func_2B]We're inside the Cursed Maze
[WAIT][NEXT][...]
Our Oxygen will be limited in
this place, so keep that in
mind!
[WAIT][NEXT][...]
If the timer reaches Zero, your
HP will start to decrease until
you die or leave the Maze.
[WAIT][NEXT][...]
If that happen, you will lose
all your Star Pieces from this
run.
[WAIT][NEXT][...]
Remember is always better to
escape from here on time!
[WAIT][END]
}
