#define .Npc_Mistar 0
#define .Npc_Cheato 1

#new:Header $Header
{
[MainScript] $Script_Main
[Background] 00000000
[EntryList] $EntryList
[EntryCount] 5
[MapTattle]  $Function_GetTattle
}

#new:EntryList $EntryList
{
~Vec4f:Entry0 % Go back
~Vec4f:Entry1 % Blue pipe
~Vec4f:Entry2 % Save Block
~Vec4f:Entry3 % Final Boss
~Vec4f:Entry4 % Game Saved (after beating the game)
}

% Override texture package
#new:Function_Init $Function_Init
{
PUSH 	RA, A0, A1
LIA 	A0, 800B0CF0
LIA 	A1, "arn_tex"
JAL 	~Func:sprintf
RESERVED
CLEAR   V0
JPOP 	RA, A0, A1
}

#new:Script_Main $Script_Main
{
Call    SetSpriteShading  	( -1 )
Call    SetCamPerspective 	( .Cam:Default 00000003 00000019 00000010 00001000 )
Call    SetCamBGColor     	( .Cam:Default 0 0 0 ) % black
Call    SetCamEnabled       ( .Cam:Default .True )
Call    SetCamLeadPlayer    ( .Cam:Default .False )
Call    SetMusicTrack 		( .Default A4 00000000 00000008 ) % Black Pit Theme
% Hide Pipe
If 	*BlackPit_BluePipe == .False
	Call	EnableModel ( ~Model:dokan_e2 .False )
	Call 	ModifyColliderFlags ( .False ~Collider:o307 7FFFFE00 )
	Call 	ModifyColliderFlags ( .False ~Collider:exit1 7FFFFE00 )
EndIf
Exec    $Script_Init_TexturePanning
Exec    $Script_MakeEntities
Exec    $TrackEntry
If *BlackPit_CheatoCutscene == .False
    Call    MakeNpcs ( 0 $NpcGroupList_Cheato )
    Exec    $Script_CheatoWaitForPlayer
EndIf
Wait    5
Call    GetEntryID ( *Var[0] )
If *Var[0] == 4 % Game Saved (after beating the game)
    Call    SetPlayerPos ( ~Vec3d:Entry4 )
    Call    TeleportPartnerToPlayer
    Call    ResetCam ( .Cam:Default *Fixed[50.0] )
    ExecWait $MovePartnerBehindMario
EndIf
Return
End
}

#new:Script $Script_Init_TexturePanning
{
    Call     EnableTexPanning 	( ~Model:bg .True )
	Thread
		Set  *Var[0]  .Default % panner id
		% Texture Pan Rate
		Set  *Var[1]  452` 	    % Main U
		Set  *Var[2]  280`      % Main V
		Set  *Var[3]  0 	    % Aux U
		Set  *Var[4]  0 	    % Aux V
		% Delay Between Updates
		Set  *Var[5]  .False	% Main U
		Set  *Var[6]  .False	% Main V
		Set  *Var[7]  .False	% Aux U
		Set  *Var[8]  .False	% Aux V
		% Initial Offset
		Set  *Var[9]  .False	% Main U 
		Set  *Var[A]  .False	% Main V
		Set  *Var[B]  .False 	% Aux U
		Set  *Var[C]  .False 	% Aux V
		ExecWait $Script_TexturePanning % from globals/patch/UtilityFunctions.patch
    EndThread
	Return
	End
}

#new:Script $Script_MakeEntities
{
    Call     MakeEntity  	( .Entity:SavePoint ~Vec4d:saveblock 80000000 )
	Call     MakeEntity  	( .Entity:HealingBlock ~Vec4d:healblock 80000000 )
	Call     AssignScript	( $Script_HealSP )
    If  *BlackPit_BluePipe == .False
        Call     MakeEntity  	( .Entity:BlueSwitch ~Vec4d:blues 80000000 )
        Call     AssignScript	( $Script_SpawnPipe )
    EndIf
	Return
	End
}

#new:Script $Script_HealSP
{
    Call    FullyRestoreSP
    Return
    End
}

#new:Script $Script_SpawnPipe
{
	Call    DisablePlayerInput ( .True )
	Call	ParentColliderToModel 	( ~Collider:o307 ~Model:dokan_e2 )
	Call	ParentColliderToModel 	( ~Collider:exit1 ~Model:dokan_e2 )
	Call	GetPlayerPos 	( .False *Var[A] .False )
	Call 	EnableModel 	( ~Model:dokan_e2 .True )
	Call 	MakeLerp		( 50` 0` 30` .Easing:CosInOut )
	Call 	PlaySound   	( 208E ) % Pipe Spawning Sound
	Label	2
	Call	UpdateLerp
	Set		*Var[2]  0
	Sub		*Var[2]  *Var[0]
	Call	TranslateModel	( ~Model:dokan_e2 0 *Var[2] 0 )
    % update colliders
	Call	UpdateColliderTransform	( ~Collider:o307 )
	Call	UpdateColliderTransform	( ~Collider:exit1 )
	Call    ModifyColliderFlags 	( .True ~Collider:o307 7FFFFE00 )
	Call    ModifyColliderFlags 	( .True ~Collider:exit1 7FFFFE00 )
	Wait	1 
	If	*Var[1] ==  1 
		Goto	2
	EndIf
	Wait	1
	Call    DisablePlayerInput ( .False )
	Set  	*BlackPit_BluePipe .True
	Return
	End
}

#new:Script $Enter_HorizontalPipe
{
	Call 	 $WriteBitFlag ( 8010EFCC .False 2 5 .True ) % Make player invisible
	Set     *Var[A]  ~Entry:Entry1 % Entry ID
    Set     *Var[B]  ~Collider:exit1 % Collider ID
    Set     *MapVar[0] .True
    Call     DisablePlayerPhysics 	( .True )
	Call     HidePlayerShadow 	( .True )
	Set		 *Var[0]  *Var[A] 
	Call     $Function_GetColliderPos
	Call     PlayerMoveTo 	( *Var[1] *Var[3] 3 )
	Set		 *Var[0]  *Var[A] 
	Call     $Function_GetColliderPos
	Call     SetPlayerPos 	( *Var[1] *Var[2] *Var[3] )
	Call     SetPlayerFlagBits 	( 00200000 .True )
	Call     $Function_SetAnimation
	Call     InterpPlayerYaw 	( *Var[0] 0 )
	Wait     2
	Call     SetPlayerFlagBits 	( 00200000 .False )
	Call     PlaySound   	( 0163 ) % Pipe sfx
	Call     GetPlayerPos 	( *Var[0] *Var[1] *Var[2] )
    Add      *Var[1] -40` % y
    Call     SetPlayerPos 	( *Var[0] *Var[1] *Var[2] )
	Call 	 $WriteBitFlag ( 8010EFCC .False 2 5 .False ) % Make player visible
    Wait     4
    Call     802D286C ( 00000100 ) % SetAnimationFlags
	Call     802D2520 ( 00010002 5 2 1 1 .False ) % SetPlayerAnimationFX
    Loop 40` 
        Add     *Var[1] 1 % y
        Call    SetPlayerPos 	( *Var[0] *Var[1] *Var[2] )
        Wait    1
    EndLoop
    Wait    5
    Call    802D2520 ( 00010000 .False .False .False .False .False )
	Call    SetPlayerAnimation 	( .PlayerAnim:StandStill )
    Call    DisablePlayerPhysics 	( .False )
	Call    HidePlayerShadow 	( .False )
    Loop
        % Set MapVar0 always as false until the player falls
        Call    $ReadAddress ( 8015A552 1 *Var[0] .False .False )
        If *Var[0] == FFFF
            Set     *MapVar[0] .False
            BreakLoop
        EndIf
        Wait    1
    EndLoop
    Return
    End
}

#new:Script $Leave_HorizontalPipe
{
    Call     DisablePartnerAI ( .True )
    Call     DisablePlayerPhysics 	( .True )
	Call     HidePlayerShadow 	( .True )
	Set		 *Var[0]  *Var[A] 
	Call     $Function_GetColliderPos
	Call     PlayerMoveTo 	( *Var[1] *Var[3] 3 )
	Set		 *Var[0]  *Var[A] 
	Call     $Function_GetColliderPos
	Call     SetPlayerPos 	( *Var[1] *Var[2] *Var[3] )
	Call     SetPlayerFlagBits 	( 00200000 .True )
	Call     $Function_SetAnimation
	Call     InterpPlayerYaw 	( *Var[0] 0 )
	Wait     2
	Call     SetPlayerFlagBits 	( 00200000 .False )
	Call     PlaySound   	( 0163 ) % Pipe sfx
	Call     GetPlayerPos 	( *Var[0] *Var[1] *Var[2] )
    Call     802D286C ( 00000800 ) % SetAnimationFlags
	Call     802D2520 ( 00010002 5 2 1 1 .False ) % SetPlayerAnimationFX
    Thread
        Loop 40`
            Add     *Var[1] -1 % y
            Call    SetPlayerPos 	( *Var[0] *Var[1] *Var[2] )
            Wait    1
        EndLoop
    EndThread
    Wait     25`
    Return
    End
}

#new:Function $Function_GetColliderPos
{
    0:  ADDIU     SP, SP, FFE0
    4:  SW        S1, 14 (SP)
    8:  DADDU     S1, A0, R0
    C:  SW        RA, 1C (SP)
   10:  SW        S2, 18 (SP)
   14:  JAL       8005ABE8
   18:  SW        S0, 10 (SP)
   1C:  DADDU     A0, S1, R0
   20:  LIO       A1, *Var[0]
   28:  JAL       ~Func:get_variable
   2C:  DADDU     S2, V0, R0
   30:  DADDU     A0, S1, R0
   34:  LUI       A1, FE36
   38:  LW        V1, 14 (S2)
   3C:  SLL       S0, V0, 4
   40:  ADDU      V1, S0, V1
   44:  LWC1      F0, 0 (V1)
   48:  TRUNC.W.S F2, F0
   4C:  MFC1      A2, F2
   50:  JAL       ~Func:set_variable
   54:  ORI       A1, A1, 3C81
   58:  DADDU     A0, S1, R0
   5C:  LW        V0, 14 (S2)
   60:  LUI       A1, FE36
   64:  ADDU      V0, S0, V0
   68:  LWC1      F0, 4 (V0)
   6C:  TRUNC.W.S F2, F0
   70:  MFC1      A2, F2
   74:  JAL       ~Func:set_variable
   78:  ORI       A1, A1, 3C82
   7C:  DADDU     A0, S1, R0
   80:  LW        V0, 14 (S2)
   84:  LUI       A1, FE36
   88:  ADDU      V0, S0, V0
   8C:  LWC1      F0, 8 (V0)
   90:  TRUNC.W.S F2, F0
   94:  MFC1      A2, F2
   98:  JAL       ~Func:set_variable
   9C:  ORI       A1, A1, 3C83
   A0:  DADDU     A0, S1, R0
   A4:  LW        V0, 14 (S2)
   A8:  LUI       A1, FE36
   AC:  ADDU      S0, S0, V0
   B0:  LWC1      F0, C (S0)
   B4:  TRUNC.W.S F2, F0
   B8:  MFC1      A2, F2
   BC:  JAL       ~Func:set_variable
   C0:  ORI       A1, A1, 3C84
   C4:  LW        RA, 1C (SP)
   C8:  LW        S2, 18 (SP)
   CC:  LW        S1, 14 (SP)
   D0:  LW        S0, 10 (SP)
   D4:  ADDIU     V0, R0, 2
   D8:  JR        RA
   DC:  ADDIU     SP, SP, 20
}

#new:Function $Function_SetAnimation
{
    0:  LAW       V1, 80077410
    8:  LIF       F12, 180.0
   10:  ADDIU     SP, SP, FFE8
   14:  SW        RA, 14 (SP)
   18:  SW        S0, 10 (SP)
   1C:  SLL       V0, V1, 2
   20:  ADDU      V0, V0, V1
   24:  SLL       V0, V0, 2
   28:  SUBU      V0, V0, V1
   2C:  SLL       V1, V0, 3
   30:  ADDU      V0, V0, V1
   34:  SLL       V0, V0, 3
   38:  LTF       F0, V0 (800B1DEC)
   44:  ADD.S     F12, F0, F12
   48:  JAL       ~Func:clamp_angle
   4C:  DADDU     S0, A0, R0
   50:  TRUNC.W.S F2, F0
   54:  SWC1      F2, 84 (S0)
   58:  LW        RA, 14 (SP)
   5C:  LW        S0, 10 (SP)
   60:  ADDIU     V0, R0, 2
   64:  JR        RA
   68:  ADDIU     SP, SP, 18
}

#new:Script $TrackEntry
{
    Call    GetLoadType ( *Var[0] )
    If *Var[0] == 1 % Save Block
        Exec    $Script_MakeExits
        Exec    EnterSavePoint
        Return
    EndIf
    Call     GetEntryID  	( *Var[0] )
    Switch *Var[0]
        Case == 0 % Go back
            Set     *Var[0] $Script_MakeExits
            Exec    EnterWalk
        Case == 1 % Pipe
            Set     *Var[A]  ~Entry:Entry1 % Entry ID
            Set     *Var[B]  ~Collider:exit1 % Collider ID
            Exec    $Enter_HorizontalPipe
            Exec    $Script_MakeExits
    EndSwitch
    Return
    End
}

#new:Script $Script_MakeExits
{
Bind     $Script_Exit0  .Trigger:FloorAbove ~Collider:exit0 00000001 00000000 % Left
Bind     $Script_Exit1  .Trigger:FloorAbove ~Collider:exit1 00000001 00000000 % Pipe
Bind     $Script_Exit2  .Trigger:WallPressA ~Collider:exit2 00000001 00000000 % Final Boss
Return
End
}

#new:Script $Script_Exit0
{
	Call     UseExitHeading  	( 60` 0 )
	Exec     ExitWalk
	Call     GotoMap	( "bp_02" 1 )
	Wait     100`
	Return
	End
}

#new:Script $Script_Exit1
{
	Set  *Var[A]  ~Entry:Entry1 % Entry ID
	Set  *Var[B]  ~Collider:exit1 % Collider ID
    ExecWait $Script_TopPipe_TrackJoystick
	Return
	End
}

#new:Script $Script_TopPipe_TrackJoystick
{
	Call     $Function_TrackJoystick
	If  *Var[0]  ==  .False
		Return
	EndIf
    % If the player came from a pipe and hasn't leave the pipe
    If *MapVar[0] == .True
        Return
    EndIf
	Call     GetCurrentPartner ( *Var[0] )
	If  *Var[0]  !=  .Partner:Empty
		Call     GetCurrentPartnerID 	( *Var[1] )
		If  *Var[1]  !=  .Partner:Watt
			Return
		Else
			Call     802D2B6C
			Call     DisablePlayerInput 	( .True )
		EndIf
	Else
		Call     DisablePlayerInput 	( .True )
	EndIf
	Unbind
	ExecWait    $Leave_HorizontalPipe
    Call    	GotoMap ( "bp_01" 2 )
	Return
	End
}

#new:Function $Function_TrackJoystick
{
    0:  LAH       V1, 8015A552
    8:  ADDIU     SP, SP, FFE8
    C:  SW        S0, 10 (SP)
   10:  DADDU     S0, A0, R0
   14:  SW        RA, 14 (SP)
   18:  LW        V0, B0 (S0)
   1C:  BEQ       V1, V0, .o2C
   20:  ADDIU     V0, R0, 2
   24:  BEQ       R0, R0, .oAC
   28:  SW        R0, 84 (S0)
        .o2C
   2C:  LAW       V1, 8007419C
   34:  LB        V0, 40 (V1)
   38:  LB        A3, 44 (V1)
   3C:  BLTZL     V0, .o44
   40:  SUBU      V0, R0, V0
        .o44
   44:  BNE       V0, R0, .o54
   48:  NOP
   4C:  BEQL      A3, R0, .oAC
   50:  DADDU     V0, R0, R0
        .o54
   54:  MTC1      V0, F4
   58:  NOP
   5C:  CVT.S.W   F4, F4
   60:  MFC1      A2, F4
   64:  MTC1      A3, F4
   68:  NOP
   6C:  CVT.S.W   F4, F4
   70:  MTC1      R0, F12
   74:  MFC1      A3, F4
   78:  JAL       ~Func:atan2
   7C:  MOV.S     F14, F12
   80:  LIF       F2, 60.0
   88:  NOP
   8C:  C.LT.S    F0, F2
   90:  NOP
   94:  BC1F      .oA8
   98:  ADDIU     V0, R0, 1
   9C:  SW        V0, 84 (S0)
   A0:  BEQ       R0, R0, .oAC
   A4:  ADDIU     V0, R0, 2
        .oA8
   A8:  DADDU     V0, R0, R0
        .oAC
   AC:  LW        RA, 14 (SP)
   B0:  LW        S0, 10 (SP)
   B4:  JR        RA
   B8:  ADDIU     SP, SP, 18
}

#new:Script $Script_Exit2
{
    Call    DisablePlayerInput    ( .True )
    Call	PlaySound 	( 002F )
    % Door animation
    Set     *Var[0] 0
    Set     *Var[1] 0
    Call    ShakeCam ( .Cam:Default 0 30` *Fixed[1.2] )
    Wait    20`
    Loop 5 % open doors
        Add     *Var[0] -15`
        Add     *Var[1] 15`
        Call    RotateModel ( ~Model:doorL *Var[0] 0 1 0 )
        Call    RotateModel ( ~Model:doorR *Var[1] 0 1 0 )
        Call    PlaySound   ( 00DD )
        Call	PlaySound 	( 002F )
        Call    ShakeCam ( .Cam:Default 0 10` *Fixed[1.1] )
        Wait    15`
    EndLoop
    Call    GetColliderCenter ( ~Collider:exit2_s )
    Call    SetPlayerSpeed ( *Fixed[1.5] )
    Call    PlayerMoveTo ( *Var[0] *Var[2] 0 )
    Set     *Var[0] -75`
    Set     *Var[1] 75`
    Set     *Var[2] 5
    Loop *Var[2] % close doors
        Add     *Var[0] 15`
        Add     *Var[1] -15`
        Call    RotateModel ( ~Model:doorL *Var[0] 0 1 0 )
        Call    RotateModel ( ~Model:doorR *Var[1] 0 1 0 )
        Call    PlaySound   ( 00DD )
        Call	PlaySound 	( 002F )
        If  *Var[2] != 1
            Call    ShakeCam ( .Cam:Default 0 10` *Fixed[1.1] )
            Wait    15`
        Else % last offset do a different shake
            Call    ShakeCam ( .Cam:Default 0 35` *Fixed[1.1] )
        EndIf
    EndLoop
    Call    GotoMapSpecial ( "bp_04" 0 4 )
	Wait    100`
	Return
	End
}

#new:Script $Script_CheatoWaitForPlayer
{
    Call    AwaitPlayerApproach ( 65` 2` 247` )
    Call    SetNpcVar ( .Npc_Cheato 0 .True ) % start cutscene
    Return
    End
}

#new:NpcGroupList $NpcGroupList_Cheato
{
00000001 $NpcGroup_Mistar 	00000000
00000001 $NpcGroup_Cheato 	00000000
00000000 00000000 00000000 
}

#new:NpcGroup $NpcGroup_Mistar
{
00000000 $Cheato_Settings 0.0 1000.0 0.0
01000001 $Script_null 00000000 00000000 0000010E 
~NoItems ~NoHP ~NoFP ~NoCoinBonus ~NoMovement
00201401 00201401 00201401 00201401 00201401 00201401 00201401 00201401
00201401 00201401 00201401 00201401 00201401 00201401 00201401 00201401
00000000 00000000 00000000 00000000
}

#new:NpcGroup $NpcGroup_Cheato
{
00000001 $Cheato_Settings ~Vec3f:cheato_spawn
01000001 $Script_Init_Cheato 00000000 00000000 00000000 
~NoItems ~NoHP ~NoFP ~NoCoinBonus ~NoMovement
00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 
00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 00AE0001 
00000000 00000000 00000000 00000000
}

#new:NpcSettings $Cheato_Settings
{
00000000 00200018 00000000 00000000 00000000 00000000 00000000 00000000
00000000 00000000 00630000 
}

#new:Script $Script_null
{
	Return
	End
}

#new:Script $Script_Init_Cheato
{
	Call     BindNpcIdle	( .Npc:Self $Script_Cheato_Idle )
	Return
	End
}

#new:Script $Script_Cheato_Idle
{
     Loop
          Call      GetSelfVar ( 0 *Var[0] )
          If *Var[0] == .True
               BreakLoop
          EndIf
          Wait 1
     EndLoop
     % Cheato Cutscene
     Exec $Script_Cutscene_Cheato_Main
     Return
     End
}

#new:Script $Script_Cutscene_Cheato_Main
{
    Call    DisablePlayerInput ( .True )
    ExecWait $PlayerStopUsingPartner
    ExecWait $MovePartnerBehindMario
    % Cutscene - Part 1
    Exec	$Script_Cutscene_bp_Cheato1 *Var[A]
    Exec	$SkipCutscene_Cheato1
    Loop
        DoesScriptExist ( *Var[A] *Var[0] )
        If *Var[0] == .False
            BreakLoop
        EndIf
        Wait 1
    EndLoop
    Set 	*Flag_Skip .False
    Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
    % cheato appears
    Call	GetPlayerPos ( *Var[0] *Var[1] *Var[2] )
    Add     *Var[0] 200`
    Call    SetNpcPos ( .Npc_Cheato *Var[0] *Var[1] *Var[2] )
    Call	SetNpcAnimation	( .Npc_Cheato 00AE0003 ) % Running
    Call	GetPlayerPos ( *Var[0] *Var[1] *Var[2] )
    Add     *Var[0] 90`
    Call    SetNpcSpeed ( .Npc_Cheato *Fixed[3.0] )
    Call    SetNpcFlagBits ( .Npc_Cheato 00000040 .True ) % ignore collisions
    Call    NpcMoveTo ( .Npc_Cheato *Var[0] *Var[2] 0 )
    % Cutscene - Part 2
    Exec	$Script_Cutscene_bp_Cheato2 *Var[A]
    Exec	$SkipCutscene_Cheato2
    Loop
        DoesScriptExist ( *Var[A] *Var[0] )
        If *Var[0] == .False
            BreakLoop
        EndIf
        Wait 1
    EndLoop
    Set 	*Flag_Skip .False
    Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
    % fade screen
    SetF	*Var[0] *Fixed[25.0]  % speed
    SetF	*Var[1] *Fixed[255.0] % fully dark
    ExecWait $Script_FadeScreenOut
    Wait    40`
    Call    SetMusicTrack 	   ( .Default .Song:PrisonerPeachTheme 00000000 00000008 )
    Call    SetPlayerAnimation ( 00010002 ) % idle
    Call    SetPlayerAnimation ( 0001002B ) % moving arms
    Call	SetNpcAnimation	( .Npc_Mistar 0020140E ) % *M*
    SetF	*Var[0] *Fixed[25.0] % speed
    SetF	*Var[1] *Fixed[5.0]  % fully bright
    ExecWait $Script_FadeScreenIn
    % Cutscene - Part 3
    Exec	$Script_Cutscene_bp_Cheato3 *Var[A]
    Exec	$SkipCutscene_Cheato3
    Loop
        DoesScriptExist ( *Var[A] *Var[0] )
        If *Var[0] == .False
            BreakLoop
        EndIf
        Wait 1
    EndLoop
    Set 	*Flag_Skip .False
    Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
    Call    GetPlayerPos    ( *Var[0] *Var[1] *Var[2] )
    Add     *Var[0] 40`
    Add     *Var[2] 20`
    Wait    35`
    Call    InterpNpcYaw    ( .Npc_Cheato -90` 0 )
    Wait    10`
    Call	SetNpcAnimation	( .Npc_Cheato 00AE0003 ) % Running
    Call    SetNpcSpeed ( .Npc_Cheato *Fixed[2.0] )
    Call    NpcMoveTo ( .Npc_Cheato *Var[0] *Var[2] 0 )
    Call    SetNpcSpeed ( .Npc_Cheato *Fixed[3.0] )
    % Cutscene - Part 4
    Exec	$Script_Cutscene_bp_Cheato4 *Var[A]
    Exec	$SkipCutscene_Cheato4
    Loop
        DoesScriptExist ( *Var[A] *Var[0] )
        If *Var[0] == .False
            BreakLoop
        EndIf
        Wait 1
    EndLoop
    Set 	*Flag_Skip .False
    Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
    Call	SetNpcAnimation	( .Npc_Mistar 00201401 ) % idle
    Call    GetPlayerPos ( *Var[0] .False *Var[1] )
    Call    GetNpcPos    ( .Npc_Mistar *Var[2] *Var[3] *Var[4] )
    Add     *Var[0] 115` % mario x pos
    Add     *Var[2] 115` % twink x pos
    Call    SetPlayerSpeed ( *Fixed[1.0] )
    Call    DisablePlayerPhysics ( .True )
    Call    SetNpcFlagBits ( .Npc_Cheato 00000100 .True ) % disable npc collision
    Thread % For twink
        Call    NpcFlyTo ( .Npc_Mistar *Var[2] *Var[3] *Var[1] 50` 0 .Easing:Linear )
    EndThread
    Call    PlayerMoveTo ( *Var[0] *Var[1] 50` )
    Call    InterpNpcYaw ( .Npc_Mistar -90` 0 )
    Call    InterpPlayerYaw ( -90` 0 )
    % Cutscene - Part 5
    Exec	$Script_Cutscene_bp_Cheato5 *Var[A]
    Exec	$SkipCutscene_Cheato5
    Loop
        DoesScriptExist ( *Var[A] *Var[0] )
        If *Var[0] == .False
            BreakLoop
        EndIf
        Wait 1
    EndLoop
    Set 	*Flag_Skip .False
    Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
    Call    InterpPlayerYaw ( 90` 0 )
    Call    InterpNpcYaw    ( .Npc_Mistar 90` 0 )
    Call    InterpNpcYaw    ( .Npc:Partner 90` 0 )
    % Screen fades
    SetF	*Var[0] *Fixed[25.0]  % speed
    SetF	*Var[1] *Fixed[255.0] % fully dark
    ExecWait $Script_FadeScreenOut
    % Make npcs invisible and disable shadows
    Call    EnableNpcShadow ( .Npc_Mistar .False )
    Call    EnableNpcShadow ( .Npc_Cheato .False )
    Call    SetNpcFlagBits ( .Npc_Mistar 81000100 .True )
    Call    SetNpcFlagBits ( .Npc_Cheato 81000100 .True )
    Call    DisablePlayerPhysics ( .False )
    Call    ResetCam ( .Cam:Default *Fixed[20.0] )
    Wait	10`
    % Fade Screen Brightness
    SetF	*Var[0] *Fixed[25.0] % speed
    SetF	*Var[1] *Fixed[5.0]  % fully bright
    ExecWait $Script_FadeScreenIn
    Call    DisablePlayerInput ( .False )
    Set     *BlackPit_CheatoCutscene .True
    Return
    End
}

#new:Script $Script_Cutscene_bp_Cheato1
{   
    % Spawn Mistar
    Call    SetNpcYaw ( .Npc_Mistar 90` )
    Call    SetNpcYaw ( .Npc_Cheato -90` )
    Call    GetPlayerPos ( *Var[0] *Var[1] *Var[2] )
    Set		*Var[3] *Var[1]
    Add     *Var[0] -25` % x
	Add		*Var[3] 20` % mistar y pos
    Add		*Var[1] 30` % sparke fx y pos
    Add     *Var[2] 20` % z
	Call	SetNpcPos ( .Npc_Mistar *Var[0] *Var[3] *Var[2] )
	Call	PlaySound	 ( 0241 ) % Focus
    Call    PlayEffect   ( ~FX:Sparkles:Star *Var[0] *Var[1] *Var[2] 20` )
    Wait    10`
    Call    PlaySoundAtPlayer ( 0263 .Default ) % ?
    Call    ShowEmote       ( .Npc_Mistar .Emote:Question -45` 20` 1 0 0 0 0 )
    Set     *Var[0] 00080016 % surprised
    Set     *Var[1] 2
    Set     *Var[2] 35`
    ExecWait $SetPlayerAnimation
    Return
    End
}

#new:Script $Script_Cutscene_bp_Cheato2
{
    % cheato speaks
    Call	SetNpcAnimation	( .Npc_Cheato 00AE0001 ) % idle
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato1 )
    Call	SpeakToPlayer	( .Npc_Mistar 0020140A 00201401 00000004 $String_bp_Mistar1 )
    Call	SetNpcAnimation	( .Npc_Mistar 00201402 ) % angry idle
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0001 00AE0001 00000000 $String_bp_Cheato2_1 )
    Wait    10`
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato2_2 )
    Call    SetPlayerAnimation ( 00010028 ) % uh?
    Call	SetNpcAnimation	( .Npc_Mistar 00201401 ) % angry idle
    Call	SpeakToPlayer	( .Npc_Mistar 0020140A 00201401 00000004 $String_bp_Mistar2 )
    Call    SetPlayerAnimation ( 00010002 ) % idle
    Call    InterpNpcYaw    ( .Npc_Cheato 90` 0 )
    Wait    25`
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0001 00AE0001 00000001 $String_bp_Cheato3 )
    Call	SpeakToPlayer	( .Npc_Mistar 0020140A 00201401 00000004 $String_bp_Mistar3 )
    Call    InterpNpcYaw    ( .Npc_Cheato -90` 0 )
    Wait    15`
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato4 )
    Return
    End
}

#new:Script $Script_Cutscene_bp_Cheato3
{
    % conversation continues
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato5 )
    Call	SetNpcAnimation	( .Npc_Mistar 00201402 ) % angry idle
    Set     *Var[0] 0001002D % cough cough
    Set     *Var[1] .False
    Set     *Var[2] 35`
    ExecWait $SetPlayerAnimation
    Call    InterpNpcYaw    ( .Npc_Cheato 90` 0 )
    Wait    15`
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000001 $String_bp_Cheato6 )
    Call	SetNpcAnimation	( .Npc_Mistar 00201410 ) % sad
    Wait    10`
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000001 $String_bp_Cheato7 )
    Return
    End
}

#new:Script $Script_Cutscene_bp_Cheato4
{
    Call	SetNpcAnimation	( .Npc_Cheato 00AE0001 ) % idle
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato8 )
    Call	SetNpcAnimation	( .Npc_Mistar 00201406 ) % angry idle
    Set     *Var[0] 00010028 % uh?
    Set     *Var[1] .False
    Set     *Var[2] 35`
    ExecWait $SetPlayerAnimation
    Call	SetNpcAnimation	( .Npc_Mistar 00201408 ) % charging
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato9 )
    Call    InterpNpcYaw    ( .Npc_Cheato -90` 0 )
    Call	SpeakToPlayer	( .Npc_Mistar 0020140A 00201408 00000004 $String_bp_Mistar4 )
    Call    SetPlayerAnimation ( 00010002 ) % idle
    Call    SetPlayerAnimation ( 00010006 ) % angry
    Call	SpeakToPlayer	( .Npc_Mistar 0020140D 00201408 00000004 $String_bp_Mistar5 )
    Call    SetMusicTrack 	   ( .Default .Song:PeachWishes 00000000 00000008 )
    Set     *Var[0] .PlayerAnim:NodYes
    Set     *Var[1] .False
    Set     *Var[2] 35`
    ExecWait $SetPlayerAnimation
    Call    SetPlayerAnimation ( 00010002 ) % idle
    Call    SetPlayerAnimation ( 00010006 ) % angry
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato10 )
    Call	SpeakToPlayer	( .Npc_Mistar 0020140D 00201408 00000004 $String_bp_Mistar6 )
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato11 )
    % mario leaves
    Call	SetNpcAnimation	( .Npc_Mistar 00201413 ) % wink
    Set     *Var[0] .PlayerAnim:ThumbsUp
    Set     *Var[1] .False
    Set     *Var[2] 35`
    ExecWait $SetPlayerAnimation
    Return
    End
}

#new:Script $Script_Cutscene_bp_Cheato5
{
    Wait    8
    Exec    $MovePartnerBehindMario
    Wait    3
    Call    SpeakToPlayer	( .Npc_Cheato 00AE0004 00AE0001 00000000 $String_bp_Cheato12 )
    Set     *Var[0] .PlayerAnim:NodYes
    Set     *Var[1] .False
    Set     *Var[2] 25`
    ExecWait $SetPlayerAnimation
    Set     *Var[0] .PlayerAnim:Pray
    Set     *Var[1] .False
    Set     *Var[2] 35`
    ExecWait $SetPlayerAnimation
    Return
    End
}

% Skippers
#new:Script $SkipCutscene_Cheato1
{
	Set 	*Flag_Skip .True
	Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
	Loop % track Start Button
		Call 	$ReadAddress ( $SkipCs_Data 0 *Var[0] .False .False )
		If *Var[0] >= 3B % Start Button Holded Progress % 3B = Max
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 1st try
			Set 	*Flag_Skip .False
			% Fade Screen Brightness
			SetGroup 0
			SuspendAll 2
			SetF	*Var[0] *Fixed[25.0]  % speed
			SetF	*Var[1] *Fixed[255.0] % fully dark
			ExecWait $Script_FadeScreenOut
            Call	SetPlayerAnimation ( .PlayerAnim:StandStill ) % idle
            Call    GetPlayerPos ( *Var[0] *Var[1] *Var[2] )
            Add     *Var[0] -25` % x
            Add		*Var[1] 20` % mistar y pos
            Add     *Var[2] 20` % z
            Call	SetNpcPos ( .Npc_Mistar *Var[0] *Var[1] *Var[2] )
			Wait	10`
			Thread % Fade Screen Brightness
				SetF	*Var[0] *Fixed[25.0] % speed
				SetF	*Var[1] *Fixed[5.0]  % fully bright
				ExecWait $Script_FadeScreenIn
			EndThread
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 2nd try
			Kill	*Var[A]
			ResumeAll 2
		EndIf
		Wait 1
		If *Flag_Skip == .False
			BreakLoop
		EndIf
	EndLoop
	Return
	End
}

#new:Script $SkipCutscene_Cheato2
{
	Set 	*Flag_Skip .True
	Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
	Loop % track Start Button
		Call 	$ReadAddress ( $SkipCs_Data 0 *Var[0] .False .False )
		If *Var[0] >= 3B % Start Button Holded Progress % 3B = Max
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 1st try
			Set 	*Flag_Skip .False
			% Fade Screen Brightness
			SetGroup 0
			SuspendAll 2
			SetF	*Var[0] *Fixed[25.0]  % speed
			SetF	*Var[1] *Fixed[255.0] % fully dark
			ExecWait $Script_FadeScreenOut
            Call	SetNpcAnimation	( .Npc_Cheato 00AE0001 ) % idle
            Call	SetNpcAnimation	( .Npc_Mistar 00201401 ) % angry idle
            Call    SetPlayerAnimation ( .PlayerAnim:StandStill )
            Wait	10`
            % Fade Screen Brightness
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 2nd try
			Kill	*Var[A]
            SetF	*Var[0] *Fixed[25.0] % speed
            SetF	*Var[1] *Fixed[5.0]  % fully bright
            ExecWait $Script_FadeScreenIn
			ResumeAll 2
		EndIf
		Wait 1
		If *Flag_Skip == .False
			BreakLoop
		EndIf
	EndLoop
	Return
	End
}

#new:Script $SkipCutscene_Cheato3
{
	Set 	*Flag_Skip .True
	Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
	Loop % track Start Button
		Call 	$ReadAddress ( $SkipCs_Data 0 *Var[0] .False .False )
		If *Var[0] >= 3B % Start Button Holded Progress % 3B = Max
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 1st try
			Set 	*Flag_Skip .False
			% Fade Screen Brightness
			SetGroup 0
			SuspendAll 2
			SetF	*Var[0] *Fixed[25.0]  % speed
			SetF	*Var[1] *Fixed[255.0] % fully dark
			ExecWait $Script_FadeScreenOut
            Call	SetNpcAnimation	( .Npc_Mistar 00201410 ) % sad
            Call    SetPlayerAnimation ( .PlayerAnim:StandStill )
			Wait	10`
			Thread % Fade Screen Brightness
				SetF	*Var[0] *Fixed[25.0] % speed
				SetF	*Var[1] *Fixed[5.0]  % fully bright
				ExecWait $Script_FadeScreenIn
			EndThread
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 2nd try
			Kill	*Var[A]
			ResumeAll 2
		EndIf
		Wait 1
		If *Flag_Skip == .False
			BreakLoop
		EndIf
	EndLoop
	Return
	End
}

#new:Script $SkipCutscene_Cheato4
{
	Set 	*Flag_Skip .True
	Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
	Loop % track Start Button
		Call 	$ReadAddress ( $SkipCs_Data 0 *Var[0] .False .False )
		If *Var[0] >= 3B % Start Button Holded Progress % 3B = Max
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 1st try
			Set 	*Flag_Skip .False
			% Fade Screen Brightness
			SetGroup 0
			SuspendAll 2
			SetF	*Var[0] *Fixed[25.0]  % speed
			SetF	*Var[1] *Fixed[255.0] % fully dark
            ExecWait $Script_FadeScreenOut
            Call	SetNpcAnimation	( .Npc_Cheato 00AE0001 ) % idle
            Call	SetNpcAnimation	( .Npc_Mistar 00201413 ) % wink
            Call    SetPlayerAnimation ( .PlayerAnim:StandStill )
            Wait	10`
            Call    SetMusicTrack 	   ( .Default .Song:PeachWishes 00000000 00000008 )
			Thread % Fade Screen Brightness
				SetF	*Var[0] *Fixed[25.0] % speed
				SetF	*Var[1] *Fixed[5.0]  % fully bright
				ExecWait $Script_FadeScreenIn
			EndThread
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 2nd try
			Kill	*Var[A]
			ResumeAll 2
		EndIf
		Wait 1
		If *Flag_Skip == .False
			BreakLoop
		EndIf
	EndLoop
	Return
	End
}

#new:Script $SkipCutscene_Cheato5
{
	Set 	*Flag_Skip .True
	Call 	$WriteAddress ( $SkipCs_Data 2 0 .False .False ) % nuke data
	Loop % track Start Button
		Call 	$ReadAddress ( $SkipCs_Data 0 *Var[0] .False .False )
		If *Var[0] >= 3B % Start Button Holded Progress % 3B = Max
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 1st try
			Set 	*Flag_Skip .False
			% Fade Screen Brightness
			SetGroup 0
			SuspendAll 2
			SetF	*Var[0] *Fixed[25.0]  % speed
			SetF	*Var[1] *Fixed[255.0] % fully dark
            ExecWait $Script_FadeScreenOut
            Call	SetNpcAnimation	( .Npc_Cheato 00AE0001 ) % idle
            Call	SetNpcAnimation	( .Npc_Mistar 00201401 ) % idle
            Call    SetPlayerAnimation ( .PlayerAnim:StandStill )
			Wait	10`
			Call 	$WriteAddress ( 80155128 0 3 0 .False ) % Close active dialog bubble - 2nd try
			Kill	*Var[A]
            % Fade Screen Brightness
            SetF	*Var[0] *Fixed[25.0] % speed
            SetF	*Var[1] *Fixed[5.0]  % fully bright
            ExecWait $Script_FadeScreenIn
			ResumeAll 2
		EndIf
		Wait 1
		If *Flag_Skip == .False
			BreakLoop
		EndIf
	EndLoop
	Return
	End
}

#string $String_bp_Cheato1
{
[STYLE:RIGHT][...]
Hey Mario, what are you doing
here?
[WAIT][END]
}

#string $String_bp_Mistar1
{
[STYLE:RIGHT][...]
Uh, we ask you the same
question, what are you doing
here?
[WAIT][END]
}

#string $String_bp_Cheato2_1
{
[STYLE:RIGHT][...]
....
[WAIT][NEXT][...]
....
[WAIT][END]
}

#string $String_bp_Cheato2_2
{
[STYLE:RIGHT][...]
I just think you should go back,
Mario.
[WAIT][END]
}

#string $String_bp_Mistar2
{
[STYLE:RIGHT][...]
You're acting really weird, what
do you know about this place!?
[WAIT][END]
}

#string $String_bp_Cheato3
{
[STYLE:RIGHT][...]
...
[WAIT][END]
}

#string $String_bp_Mistar3
{
[STYLE:RIGHT][...]
Cheato?
[WAIT][END]
}

#string $String_bp_Cheato4
{
[STYLE:RIGHT][...]
I guess at this point you
already know that we're stuck
in an endless loop inside this
place.
[WAIT][NEXT][...]
I guess...
[WAIT][NEXT][...]
I don't have any other choice...
[WAIT][NEXT][...]
I don't even know where to
start but...
[WAIT][END]
}

#string $String_bp_Cheato5
{
[STYLE:RIGHT][...]
And that's it.
[WAIT][NEXT][...]
Basically, every enemy you have
fought inside those Pits...
[WAIT][NEXT][...]
At some point they were people
trying to escape from this
place just like you.
[WAIT][NEXT][...]
But in the end, they weren't
strong enough.
[WAIT][NEXT][...]
And when this powerful being
gets bored of watching you
trying to leave this place...
[WAIT][NEXT][...]
He'll just trap you on one of
the floors of those Pits.
[WAIT][NEXT][...]
I suppose he hasn't gotten rid
of you because he has seen
something special about you...
[WAIT][NEXT][...]
But when he gets tired of you
he'll do the same...
[WAIT][END]
}

#string $String_bp_Cheato6
{
[STYLE:RIGHT][...]
I'm supposed to help everyone
who arrives here leave this
place...
[WAIT][NEXT][...]
That was the condition he gave
to me in exchange for being
able to be free...
[WAIT][NEXT][...]
But all this time I've only seen
how everyone who's arrived to
the lobby...
[WAIT][NEXT][...]
Suddenly never come back from
those Pits.
[WAIT][END]
}

#string $String_bp_Cheato7
{
[STYLE:RIGHT][...]
That's when I found out he just
got rid of him and managed to
make another poor soul fall
into this place again...
[WAIT][NEXT][...]
I... I...
[WAIT][NEXT][...]
I was supposed to help all
those people leave this place
but the only thing...
[WAIT][NEXT][...]
The only thing I've been able
to do is watch all my friends
get lost forever inside those
Pits...
[WAIT][END]
}

#string $String_bp_Cheato8
{
[STYLE:RIGHT][...]
That's why...
[WAIT][NEXT][...]
I ask you to just...
[WAIT][NEXT][...]
Go back...
[WAIT][NEXT][...]
I don't want to lose another
friend!!
[WAIT][NEXT][...]
I know you're strong Mario,
but you won't be able to
defeat him!
[WAIT][END]
}

#string $String_bp_Cheato9
{
[STYLE:RIGHT][...]
Also even if you manage to
achieve that, we don't even know
if that will let us leave this
place once and for all!
[WAIT][NEXT][...]
So maybe fighting him doesn't
even do anything...
[WAIT][END]
}

#string $String_bp_Mistar4
{
[STYLE:RIGHT][...]
I get what you're saying but do
you really think we'll just sit
here with our arms crossed?
[WAIT][NEXT][...]
We all know Mario isn't that
kind of person.
[WAIT][NEXT][...]
Do you think I haven't lost any
friends?!
[WAIT][NEXT][...]
The only option we have left is
to fight!
[WAIT][END]
}

#string $String_bp_Mistar5
{
[STYLE:RIGHT][...]
To be honest, I would prefer
to die trying to leave this place
instead of just doing nothing!
[WAIT][END]
}

#string $String_bp_Cheato10
{
[STYLE:RIGHT][...]
...
[WAIT][NEXT][...]
You're right...
[WAIT][NEXT][...]
Sorry guys.
[WAIT][NEXT][...]
We need to do it for everyone
trapped inside this place!
[WAIT][END]
}

#string $String_bp_Mistar6
{
[STYLE:RIGHT][...]
For everyone trapped on the
floors of those Pits we need to
do it!!!
[WAIT][END]
}

#string $String_bp_Cheato11
{
[STYLE:RIGHT][...]
That's right!
[WAIT][NEXT][...]
I'll be cheering for all of
you, good luck!
[WAIT][END]
}

#string $String_bp_Cheato12
{
[STYLE:RIGHT][...]
Mario...
[WAIT][NEXT][...]
Please save us...
[WAIT][END]
}

#new:Function $Function_GetTattle
{
LIO       V0, $MapTattle
JR        RA
RESERVED
}
	
#string $MapTattle
{
[STYLE:TATTLE][...]
[Func_2B]This is it, the last room
before the final battle.
[WAIT][NEXT][...]
What a journey Mario, are you
ready for this?
[WAIT][NEXT][...]
Let's go!!!
[WAIT][END]
}