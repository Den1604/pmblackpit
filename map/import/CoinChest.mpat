%% This function handle how much coins the chest will hive to Mario
%% After he obtained all available badges, the coins values is diferent
%% per floor, ej. Level 10 Chest give less coins than the one in Level 100

#new:Script $CoinChest
{
	If *MapVar[2] == FE % First Time Mario get Coins from a Chest
		Call 	$ReadBitFlag ( 8010EFCA .False .False 5 *Var[0] )
		If *Var[0] == .False % DisablePlayerInputs == False
		Call 	$WriteAddress ( 8010EFDD 0 .False 0 .False ) % Player Inputs's Byteflag
			Call 	DisablePlayerInput ( .True )
		EndIf
		Call	 ShowMessageAtScreenPos ( $CoinsString 000000A0 00000028 )
		If *Flag_Mode_Rogue == .False % only in classic
			Call	 ShowCoinCounter 	( .True )
		EndIf
		Call     ShowGotItem 	( .Item:Coin .True 00000010 ) % ItemID ??? Message
		ExecWait $LoadChestFloor
		If *Flag_Mode_Rogue == .False % only in classic
			Call	 ShowCoinCounter 	( .False )
			Switch *BadgeList % Challenge Completed
				Case >= 62`
					Set	*Flag_Cheev_Classic_07 .True
					Set	*Flag_Cheev_Classic_06 .True
					Set	*Flag_Cheev_Classic_05 .True
					Set	*Flag_Cheev_Classic_04 .True
				Case >= 40`
					Set	*Flag_Cheev_Classic_06 .True
					Set	*Flag_Cheev_Classic_05 .True
					Set	*Flag_Cheev_Classic_04 .True
				Case >= 20`
					Set	*Flag_Cheev_Classic_05 .True
					Set	*Flag_Cheev_Classic_04 .True
				Case >= 5
					Set	*Flag_Cheev_Classic_04 .True
			EndSwitch
		EndIf
		Call 	$ReadBitFlag ( 8010EFCA .False .False 5 *Var[0] )
		If *Var[0] == .True % DisablePlayerInputs == True
			Call 	$WriteAddress ( 8010EFDD 0 .True 0 .False ) % Player Inputs's Byteflag
			Call 	DisablePlayerInput ( .False )
		EndIf
	Else
		If *MapVar[2] == FF % Second Time Coins
			If *Flag_Mode_Rogue == .False % only in classic
				Call	 ShowCoinCounter 	( .True )
			EndIf
			Call     ShowGotItem 	( .Item:Coin .True 00000010 )
			ExecWait $LoadChestFloor
			If *Flag_Mode_Rogue == .False % only in classic
				Call	 ShowCoinCounter 	( .False )
				Switch *BadgeList % Challenge Completed
					Case >= 62`
						Set	*Flag_Cheev_Classic_07 .True
						Set	*Flag_Cheev_Classic_06 .True
						Set	*Flag_Cheev_Classic_05 .True
						Set	*Flag_Cheev_Classic_04 .True
					Case >= 40`
						Set	*Flag_Cheev_Classic_06 .True
						Set	*Flag_Cheev_Classic_05 .True
						Set	*Flag_Cheev_Classic_04 .True
					Case >= 20`
						Set	*Flag_Cheev_Classic_05 .True
						Set	*Flag_Cheev_Classic_04 .True
					Case >= 5
						Set	*Flag_Cheev_Classic_04 .True
				EndSwitch
			EndIf
			Set 	*Flag_Coins2 .False
			Call 	DisablePlayerInput ( .False )
		EndIf
	EndIf
	Return
	End	
}
					
#new:Script $LoadChestFloor
{
	Switch *PitMapID
		Case == 10` % pit10
			Set *Var[0] 100`
		Case == 20` % pit20
			Set *Var[0] 100`
		Case == 30` % pit30
			Set *Var[0] 100`
		Case == 40` % pit40
			Set *Var[0] 150`
		Case == 50` % ...
			Set *Var[0] 150`
		Case == 60` % ...
			Set *Var[0] 200`
		Case == 70` % ...
			Set *Var[0] 200`
		Case == 80` % ...
			Set *Var[0] 400`
		Case == 90`
			Set *Var[0] 500`
		Case == 100` % pit100
			Set *Var[0] 1000`
	EndSwitch
	If *Flag_Mode_Rogue == .False
		Call	AddCoin ( *Var[0] )
	Else % Roguelike
		% For Roguelike add to your bank account instead
		% Needed only in Floor 100
		% show bank's coin box
		Set   	*Var[A] *Var[0]
		Set		*BankTextBoxPosition 1
		Set		*TalkingToFishmael .True
		Wait 	10`
		Call 	$ReadAddress  ( 803560DC 2 *Var[1] .False .False ) % load bank account
		Add		*Var[1] *Var[0]
		SetTimescale *Fixed[15.0]
		Loop
			Call 	$ReadAddress  ( 803560DC 2 *Var[2] .False .False ) % load bank account
			Add	*Var[2] 1
			% Bank Account Cap
			If *Var[2] > 9999999` % 9,999,999 / 0098967F
				BreakLoop
			EndIf
			Call 	$WriteAddress ( 803560DC 2 *Var[2] .False .False ) % save bank account
			Call	PlaySound ( 0211 ) % coin
			If *Var[2] == *Var[1]
				BreakLoop
			EndIf
			Wait	1
		EndLoop
		SetTimescale *Fixed[1.0] % Back to Normal
		Call $ReadAddress ( 803560DC 2 *Var[1] .False .False ) % load bank account
		% Check Challenges
		Switch *Var[1]
			Case >= 40000`
				Set *Flag_Cheev_Global_13 .True
				Set *Flag_Cheev_Global_12 .True
				Set *Flag_Cheev_Global_11 .True
			Case >= 10000`
				Set *Flag_Cheev_Global_12 .True
				Set *Flag_Cheev_Global_11 .True
			Case >= 1000`
				Set *Flag_Cheev_Global_11 .True
		EndSwitch
		Call	SetMessageValue ( *Var[A] 1 )
		Call  	ShowMessageAtScreenPos  ( $String_Rogue_CoinsAdded 000000A0 00000028 )
		% close bank's coin box
		Call 	$WriteAddress ( $BoxAnimation_State  2 2 .False .False )
		Call 	$WriteAddress ( $BoxAnimation_Offset 2 7 .False .False )
		Wait	10`
	EndIf
	Return
	End
}
